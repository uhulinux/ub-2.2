### -*-hoc-*-
### /usr/local/src/hoc/hoc-7.0.0.beta/log1pb.hoc, Sat Dec  8 14:51:02 2001
### Edit by Nelson H. F. Beebe <beebe@math.utah.edu>
### ====================================================================
### Provide an implementation of, and test package for, Logp1(x).  This
### is the second trial implementation, using a Taylor series expansion
### for ln((z+1)/(z-1)).
### [08-Dec-2001]
### ====================================================================

func Log1p(x) \
{
	### Compute Log1p(x) = ln(1 + x) accurately.  The cutoffs at
	### which at least one bit is lost in substraction were
	### accurately determined in Maple with this code:
	###
	###	Digits := 50;
	###	g := proc(x) return (evalf(log(1 + x))); end proc;
	###	plot(g(x), x = -0.5..0.5);
	###	fsolve(g(x) = -0.5, x, -0.5..0);
	###	        -.39346934028736657639620046500881954655808186451281
	###	fsolve(g(x) = 0.5, x, 0..1);
	###		.64872127070012814684865078781416357165377610071015
	###
	### For small values between the cutoffs, we use the Taylor
	### series expansion, adding terms until they are small enough
	### to be ignored.
	###
	### The ___test_log1p() function produces output that looks like
	### this (on a Sun Solaris 2.7 system, using the vendor's native
	### expm1() function for comparison):
	###
	###     x               # terms         error in Log1p(x)       Ulps
	###     -0.4            0               0                       0
	###     -0.35           13              -5.55e-17              -0.25
	###     -0.3            11              0                       0
	###     -0.25           10              5.55e-17                0.25
	###     -0.2            9               0                       0
	###     -0.15           8               2.78e-17                0.125
	###     -0.1            7               1.39e-17                0.0625
	###     -0.05           6               6.94e-18                0.0312
	###     -6.94e-17       2               0                       0
	###     0.05            6               6.94e-18                0.0312
	###     0.1             7               0                       0
	###     0.15            8               -2.78e-17              -0.125
	###     0.2             9               0                       0
	###     0.3             10              -5.55e-17              -0.25
	###     0.4             11              0                       0
	###     0.45            12              1.11e-16                0.5
	###     0.55            13              5.55e-17                0.25
	###     0.65            0               -1.11e-16              -0.5
	###
	###     -0.393          14              0                       0
	###     0.649           14              -5.55e-17              -0.25
	###
	### Thus, we need from 1 (x near 0) to 14 (x near cutoffs)
	### terms of the Taylor series.  A polynomial appoximation
	### would be faster, and more efficient, but this code is
	### simple and acceptable for now.

	global k, xcutneg, xcutpos
	k = 0

	### Give names to the cutoff values:
	xcutneg = -0.39346934028736657639620046500881954655808186451281
	xcutpos = +0.64872127070012814684865078781416357165377610071015

	### Handle easy special cases first:
	if (isnan(x)) \
		return (x) \
	else if (x == 0) \
		return (0) \
	else if (x >= xcutpos) \
		return (log(1 + x)) \
	else if (x <= xcutneg) \
		return (log(1 + x)) \

	### Use Taylor series expansion (Abramowitz and Stegun, NBS#55, 4.1.28) for
	### ln((z+1)/(z-1)) = 2(1/z + 1/(3z^3) + 1/(5z^5) + ...)
	### With 1+x = (z+1)/(z-1), we find z = (1 + 2/x) and 1/z = x/(2 + x).
	k = 1
	sum = 0
	one_over_z = x/(2 + x)
	one_over_z_sq = one_over_z * one_over_z
	one_over_z_to_2km1 = one_over_z
	term = one_over_z_to_2km1
	while ((sum + term) != sum) \
	{
		sum += term
		k++
		one_over_z_to_2km1 *= one_over_z_sq
		term = one_over_z_to_2km1 / (2*k - 1)
		## print k, term, sum
		## print "\n"
	}
	sum *= 2
	return (sum)
}

func ___log1p_err() return (Log1p($1) - log1p($1))

proc ___test_log1p_print() \
{
	global xx, k, e, epsilon
	printf "%7.2e\t%7d\t%15.2e\t%15.2f\n", xx, k, e, e/epsilon
}

proc ___test_log1p() \
{
	global xx, k, e, epsilon
	k = 0
	epsilon = macheps(1)
	PREC = 3
	print "x\t\t# terms\t\terror in Log1p(x)\tUlps\n"
	last_k = -1
	for (xx = -0.4; xx < 0.7; xx += 0.05) \
	{
		e = ___log1p_err(xx)
		if (k != last_k) ___test_log1p_print()
		last_k = k
	}
	xx = xcutneg + epsilon
	e = ___log1p_err(xx)
	___test_log1p_print()

	xx = xcutpos - epsilon
	e = ___log1p_err(xx)
	___test_log1p_print()
}

proc help_Log1p() \
{
	print "\tLog1p(x) returns log(1 + x), but without accuracy loss for\n"
	print "\tsmall |x|.  x must be in (-1.0,infinity].\n\n"
}
